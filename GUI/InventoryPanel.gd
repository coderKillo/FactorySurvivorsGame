class_name InventoryPanel
extends Panel

signal held_item_changed(panel, item)

# only allow specifc item to contain
@export var item_filter := ""

var held_item: BlueprintEntity:
	set = _set_held_item

@onready var select_frame: TextureRect = $SelectFrame

@onready var _lable: Label = $Label
@onready var _mask: Node2D = $Mask

var _gui: GUI


func setup(gui: GUI):
	_gui = gui

	_update_label()


func _gui_input(event: InputEvent) -> void:
	var left_click := event.is_action_pressed("left_click")

	if not left_click:
		return

	_gui._drag_preview.on_panel_clicked(self)


func _set_held_item(blueprint: BlueprintEntity):
	if held_item and held_item.get_parent() == _mask:
		_mask.remove_child(held_item)
		held_item.stack_count_changed.disconnect(_on_held_item_stack_count_changed)

	held_item = blueprint

	if held_item:
		_mask.add_child(held_item)
		_mask.move_child(held_item, 0)
		held_item.modulate = Color.WHITE
		held_item.stack_count_changed.connect(_on_held_item_stack_count_changed)

	_update_label()
	held_item_changed.emit(self, held_item)


func _on_held_item_stack_count_changed(_count: int) -> void:
	_update_label()
	held_item_changed.emit(self, held_item)


func _update_label() -> void:
	var can_be_stacked := held_item and held_item.stack_count > 1

	if can_be_stacked:
		_lable.text = str(held_item.stack_count)
		_lable.show()
	else:
		_lable.text = str(1)
		_lable.hide()
