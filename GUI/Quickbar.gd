class_name Quickbar
extends InventoryBar

const GLOBAL_COOLDOWN := 0.5


func _ready_intern():
	Events.build_mode_changed.connect(_on_build_mode_changed)


func add_entity(blueprint: BlueprintEntity) -> void:
	for panel in panels:
		if panel.held_item == null:
			panel.held_item = blueprint
			return

	printerr("could not add entity '%s': quickbar is full!" % blueprint.name)


func select_panel(index: int) -> void:
	if not visible:
		return

	var event = InputEventMouseButton.new()
	event.button_index = MOUSE_BUTTON_LEFT
	event.pressed = true

	panels[index]._gui_input(event)

	_deselect_all()
	_select(index)


func _deselect_all():
	for panel in panels:
		panel.select_frame.hide()


func _select(index):
	panels[index].select_frame.show()


func _make_panels():
	print("make Panel")
	for i in slot_count:
		var panel := InventoryPanelScene.instantiate()
		add_child(panel)

		var inventory_panel: InventoryPanel = panel.get_node("InventoryPanel")
		var label: Label = panel.get_node("Label")

		panels.append(inventory_panel)

		var index := wrapi(i + 1, 0, slot_count)
		label.text = str(index)

		inventory_panel.held_item_changed.connect(_on_panel_item_changed)


func _on_panel_item_changed(_panel: InventoryPanel, _item: BlueprintEntity) -> void:
	for child in get_children():
		if child.has_method("start_cooldown"):
			child.start_cooldown(GLOBAL_COOLDOWN)


func _on_build_mode_changed(mode: BuildModeManager.GameMode) -> void:
	match mode:
		BuildModeManager.GameMode.BUILD_MODE:
			_pause_cooldowns(true)
			visible = true

		BuildModeManager.GameMode.NORMAL_MODE:
			_pause_cooldowns(false)
			visible = false


func _pause_cooldowns(paused: bool) -> void:
	for child in get_children():
		if child.has_method("pause_cooldown"):
			child.pause_cooldown(paused)
