class_name CraftingRecipe
extends PanelContainer

@onready
var _cost_label: Label = $MarginContainer/HBoxContainer/VBoxContainer/HBoxContainer/MarginContainer/Label
@onready var _name_label: Label = $MarginContainer/HBoxContainer/VBoxContainer/MarginContainer/Label
@onready var _sprite: GUISprite = $MarginContainer/HBoxContainer/GUISprite
@onready var _progress_bar: ProgressBar = $MarginContainer2/ProgressBar
@onready var _queue_label: Label = $MarginContainer3/Label

var _tween: Tween
var _blueprint_name := ""
var _queue_size = 0:
	set = _set_queue_size


func setup(blueprint: BlueprintEntity) -> void:
	_blueprint_name = Library.get_entity_name(blueprint)
	_name_label.text = Library.get_entity_name(blueprint).capitalize()
	_cost_label.text = str(blueprint.craft_cost)
	_queue_label.text = ""

	var sprite: Sprite2D = blueprint.get_node_or_null("Sprite2D")
	if sprite != null:
		_sprite.texture = sprite.texture
		_sprite.region_enabled = sprite.region_enabled
		_sprite.region_rect = sprite.region_rect
		# fit progress bar to panel
		_progress_bar.custom_minimum_size.y = _sprite.custom_minimum_size.y + 7


func _gui_input(event: InputEvent) -> void:
	var left_click := event.is_action_pressed("left_click")

	if not left_click:
		return

	_queue_size += 1


func _process(_delta) -> void:
	if _tween and _tween.is_running():
		return

	if _queue_size > 0:
		_queue_size -= 1
		_create_item()


func _create_item() -> void:
	_tween = create_tween()
	_tween.tween_callback(_progress_bar.show)
	_tween.tween_property(_progress_bar, "value", 1.0, 1.0).from(0.0)
	_tween.tween_callback(_progress_bar.hide)
	_tween.tween_callback(_item_created)


func _item_created() -> void:
	var blueprint = Library.blueprints[_blueprint_name].instantiate()
	Events.inventory_item_added.emit(blueprint)


func _set_queue_size(value) -> void:
	_queue_size = value
	if _queue_size > 0:
		_queue_label.text = str(value + 1) + "x"
	else:
		_queue_label.text = ""
