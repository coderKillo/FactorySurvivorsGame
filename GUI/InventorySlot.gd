class_name InventorySlot
extends RefCounted

var stack := 0:
	set = _set_stack

var data: EntityData:
	get = _get_data

var _panel: InventoryPanel = null


func is_bound() -> bool:
	return _panel != null


func bind(panel: InventoryPanel):
	_panel = panel
	_panel.held_item_changed.connect(_on_panel_item_changed)


func unbind():
	_panel.held_item_changed.disconnect(_on_panel_item_changed)
	_panel = null


func full() -> bool:
	return is_bound() and (_panel.held_item != null) and _panel.held_item.full()


func empty() -> bool:
	return stack <= 0


func _on_panel_item_changed(__panel: InventoryPanel, item: BlueprintEntity) -> void:
	if item == null:
		stack = 0
		return

	if item.stack_count != stack:
		stack = item.stack_count


func _set_stack(value):
	stack = value

	if is_bound():
		_update_panel_stack()


func _get_data() -> EntityData:
	if _panel == null or _panel.held_item == null:
		return EntityData.new()
	else:
		return _panel.held_item.data


func _update_panel_stack() -> void:
	var has_item = _panel.held_item != null
	var is_item_filter_in_library = _panel.item_filter in Library.blueprints

	if not has_item and stack > 0 and is_item_filter_in_library:
		var blueprint = Library.blueprints[_panel.item_filter].instantiate()
		blueprint.stack_count = stack
		_panel.held_item = blueprint
	elif has_item and stack <= 0:
		_panel.held_item = null
	elif has_item and _panel.held_item.stack_count != stack:
		_panel.held_item.stack_count = stack
