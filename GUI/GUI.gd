class_name GUI
extends CenterContainer

const QUICKBAR_ACTIONS := [
	"quickbar_1",
	"quickbar_2",
	"quickbar_3",
	"quickbar_4",
	"quickbar_5",
	"quickbar_6",
	"quickbar_7",
	"quickbar_8",
	"quickbar_9",
	"quickbar_0"
]

signal open_entity_ui_changed(value)

var blueprint: BlueprintEntity:
	set(value):
		_drag_preview.blueprint = value
	get:
		return _drag_preview.blueprint

var preview: Node:
	get:
		return _drag_preview._preview

var open_entity_ui: BaseGuiComponent:
	set(value):
		open_entity_ui = value
		open_entity_ui_changed.emit(value)

@onready var _quickbar: Quickbar = $MarginContainer/Quickbar
@onready var _drag_preview: DragPreview = $DragPreview
@onready var _resource: ResourceUI = $ResourceUI
@onready var _upgrade_gui: UpgradeSystemUI = $UpgradeSystemGUI
@onready var _build_mode_ui: BuildModeUI = $BuildModeUI

var _current_quickbar_index := 0


func _ready():
	_quickbar.setup(self)
	_quickbar.add_entity(Library.blueprints["Smelter"].instantiate())
	_quickbar.add_entity(Library.blueprints["PowerPlant"].instantiate())
	_quickbar.add_entity(Library.blueprints["Conveyor"].instantiate())
	_quickbar.add_entity(Library.blueprints["SpikeTrap"].instantiate())
	_resource.setup(self)


func _process(_delta):
	_drag_preview.position_mode = (DragPreview.PositionMode.SNAP)


func _unhandled_input(event: InputEvent):
	for i in QUICKBAR_ACTIONS.size():
		if InputMap.event_is_action(event, QUICKBAR_ACTIONS[i]) and event.is_pressed():
			_current_quickbar_index = i

			_quickbar.select_panel(_current_quickbar_index)

	if event.is_action_pressed("quickbar_next"):
		_current_quickbar_index += 1
		if _current_quickbar_index >= QUICKBAR_ACTIONS.size():
			_current_quickbar_index -= QUICKBAR_ACTIONS.size()

		_quickbar.select_panel(_current_quickbar_index)

	if event.is_action_pressed("quickbar_previous"):
		_current_quickbar_index -= 1
		if _current_quickbar_index < 0:
			_current_quickbar_index += QUICKBAR_ACTIONS.size()

		_quickbar.select_panel(_current_quickbar_index)


func get_quickbar_panels() -> Array:
	return _quickbar.panels


func set_build_mode_icon(pause: bool) -> void:
	_build_mode_ui.set_pause_icon(pause)


func set_quickbar_visible(value: bool) -> void:
	_quickbar.visible = value


func pause_quickbar_cooldowns(paused: bool) -> void:
	_quickbar.pause_cooldowns(paused)


func add_to_quickbar(entity: BlueprintEntity) -> void:
	return _quickbar.add_entity(entity)


func destroy_blueprint():
	_drag_preview.destroy_blueprint()


func display_upgrade(upgrades: Array[Upgrade], callback: Callable):
	_upgrade_gui.display_options(upgrades, callback)
