class_name InventoryWindow
extends MarginContainer

signal inventory_changed(panel, held_item)

var _gui: GUI
var _panels: Array[InventoryPanel] = []

@onready var inventory_path = $PanelContainer/MarginContainer/Inventories
@onready var inventories := inventory_path.get_children()


func _ready():
	Events.inventory_item_added.connect(_on_inventory_item_added)


func setup(gui: GUI):
	_gui = gui
	for inventory in inventories:
		inventory.setup(gui)
		_panels.append_array(inventory.panels)


func _on_inventory_item_added(blueprint: BlueprintEntity):
	var panel = _find_panel_not_full_stack(blueprint)

	if panel != null:
		var stack_left = panel.held_item.data.stack_size - panel.held_item.stack_count
		if stack_left >= blueprint.stack_count:
			panel.held_item.stack_count += blueprint.stack_count
			blueprint.queue_free()
			return

		else:
			panel.held_item.stack_count = panel.held_item.data.stack_size
			blueprint.stack_count -= stack_left

	panel = _find_first_empty_panel()

	if panel == null:
		print("Inventory Full!")
		return

	panel.held_item = blueprint


func _find_panel_not_full_stack(blueprint: BlueprintEntity) -> InventoryPanel:
	for panel in _panels:
		if panel.held_item == null:
			continue
		if panel.held_item.name != blueprint.name:
			continue
		if panel.held_item.stack_count >= panel.held_item.data.stack_size:
			continue
		return panel

	return null


func _find_first_empty_panel() -> InventoryPanel:
	for panel in _panels:
		if panel.held_item == null:
			return panel

	return null
