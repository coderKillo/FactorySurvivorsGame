extends VBoxContainer

@onready var _panel: InventoryPanel = $InventoryPanel
@onready var _progress_bar: TextureProgressBar = $CooldownProgress
@onready var _timer: Timer = $CooldownTimer
@onready var _cost_lable: Label = %CostLabel
@onready var _cooldown_label: Label = %CooldownLabel

var _initialized := false


func _ready():
	_progress_bar.value = 0.0
	_panel.held_item_changed.connect(_on_held_item_changed)
	_timer.timeout.connect(_end_cooldown)
	Events.total_money_changed.connect(_on_total_money_changed)


func _process(_delta):
	if _timer.time_left > 0.0:
		_progress_bar.value = _timer.time_left / _timer.wait_time
		_cooldown_label.text = str(int(_timer.time_left))
		_cooldown_label.show()
	else:
		_cooldown_label.hide()


func _on_held_item_changed(__panel: InventoryPanel, item: BlueprintEntity):
	if item == null:
		return

	var entity_name := Library.get_entity_name(item)

	if not _initialized:
		_initialize_panel(entity_name)
		_initialized = true
		return

	start_cooldown(UpgradeData.entites_data[entity_name].cooldown)


func _initialize_panel(entity_name: String) -> void:
	var cost = UpgradeData.entites_data[entity_name].energy_cost
	_cost_lable.show()
	_cost_lable.text = str(cost)


func start_cooldown(time: float) -> void:
	if _timer.time_left > 0.0:
		return

	if _panel.held_item == null:
		return

	_panel.held_item.on_cooldown = true
	_timer.start(time)


func pause_cooldown(paused: bool) -> void:
	_timer.paused = paused


func _end_cooldown() -> void:
	if _panel.held_item == null:
		return

	_panel.held_item.on_cooldown = false
	Events.slot_cooldown_finished.emit(Library.get_entity_name(_panel.held_item))


func _on_total_money_changed(money: int) -> void:
	if _panel.held_item == null:
		return

	var entity_name := Library.get_entity_name(_panel.held_item)
	var entity_cost = UpgradeData.entites_data[entity_name].energy_cost
	_panel.held_item.placeable = money >= entity_cost

	if _panel.held_item.placeable:
		modulate = Color.WHITE
	else:
		modulate = Color(1.0, 0.0, 0.0, 0.5)
