class_name LaserBeam
extends RayCast2D

@export var max_length := 200.0
@export var grow_time := 0.2

@onready var _beam: Sprite2D = $LaserBeam
@onready var _muzzle: Sprite2D = $LaserMuzzle
@onready var _particle: GPUParticles2D = $BeamParticles

var is_casting = false:
	set = _set_is_casting


func _ready():
	_beam.scale.y = 0
	_muzzle.scale.y = 0
	_particle.emitting = false
	set_physics_process(false)


func _unhandled_input(event):
	if event.is_action("left_click"):
		is_casting = true
	if event.is_action("right_click"):
		is_casting = false


func _process(_delta):
	look_at(get_global_mouse_position())


func _physics_process(_delta):
	target_position = Vector2.RIGHT * max_length
	_cast_beam()


func _set_is_casting(value):
	is_casting = value

	if is_casting:
		target_position = Vector2.ZERO
		_appear()
	else:
		_disappear()

	set_physics_process(is_casting)


func _appear():
	var tween = get_tree().create_tween()
	tween.parallel().tween_property(_beam, "scale:y", 1, grow_time)
	tween.parallel().tween_property(_muzzle, "scale:y", 1, grow_time)

	_particle.emitting = true


func _disappear():
	var tween = get_tree().create_tween()
	tween.parallel().tween_property(_beam, "scale:y", 0, grow_time)
	tween.parallel().tween_property(_muzzle, "scale:y", 0, grow_time)

	_particle.emitting = false


func _cast_beam():
	var cast_point = target_position

	force_raycast_update()
	if is_colliding():
		cast_point = get_collision_point()

	var distance = global_position.distance_to(cast_point)
	_beam.scale.x = distance - _beam.offset.x
	_particle.position.x = distance
