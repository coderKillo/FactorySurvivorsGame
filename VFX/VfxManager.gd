extends Node

var _effects = {
	"upgrade_explosion": preload("res://VFX/upgrade_explosion.tscn"),
	"destruction": preload("res://VFX/destruction.tscn"),
	"hit_effect": preload("res://VFX/hit_effect.tscn"),
	"smoke_explosive": preload("res://VFX/smoke_explosive.tscn"),
	"shatter": preload("res://VFX/shatter.tscn"),
}

var _freeze_timer: Timer


func _ready():
	process_mode = Node.PROCESS_MODE_ALWAYS

	Events.spawn_effect.connect(_on_spawn_effect)
	Events.spawn_effect_rotated.connect(_on_spawn_effect_rotated)
	Events.frame_freeze.connect(_on_frame_freeze)

	_freeze_timer = Timer.new()
	_freeze_timer.one_shot = true
	add_child(_freeze_timer)


func _on_spawn_effect(effect_name: String, effect_position: Vector2):
	_on_spawn_effect_rotated(effect_name, effect_position, 0.0)


func _on_spawn_effect_rotated(effect_name: String, effect_position: Vector2, degree: float):
	if not effect_name in _effects:
		printerr("invalid effect name: %s" % effect_name)
		return

	var effect = _effects[effect_name].instantiate() as GPUParticles2D
	effect.global_position = effect_position
	effect.rotation_degrees = degree

	add_child(effect)

	await get_tree().create_timer(effect.lifetime).timeout

	effect.queue_free()


func _on_frame_freeze() -> void:
	if _freeze_timer.time_left > 0.0:
		return

	var freeze_duration := 0.1
	var freeze_time_scale := 0.05

	Engine.time_scale = freeze_time_scale

	_freeze_timer.start(freeze_time_scale * freeze_duration)
	await _freeze_timer.timeout

	Engine.time_scale = 1.0
