extends Node

const FILE_NAME = "user://planets.tres"

var _data: PlanetResource
var _index := 0


func next_planet() -> void:
	if is_last_planet():
		printerr("Invalid: out of bounds")
		return
	_index += 1


func previous_planet() -> void:
	if is_first_planet():
		printerr("Invalid: out of bounds")
		return
	_index -= 1


func current_planet():
	return _data.planet_list[_index]


func is_first_planet() -> bool:
	return _index <= 0


func is_last_planet() -> bool:
	return _index >= (planet_count() - 1)


func planet_count() -> int:
	return _data.planet_list.size()


func _ready():
	_load_or_create()
	_data.changed.connect(_on_data_changed)


func _save():
	ResourceSaver.save(_data, FILE_NAME)


func _load_or_create():
	if ResourceLoader.exists(FILE_NAME):
		_data = ResourceLoader.load(FILE_NAME)
		return

	_data = PlanetResource.new()
	_save()


func _on_data_changed() -> void:
	print("TODO: planet_list_change")
