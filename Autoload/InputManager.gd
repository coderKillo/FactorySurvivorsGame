extends Node2D

enum Device { KEYBOARD, GAMEPAD }

var _player: Player
var _cart: Minecart
var _build_mode := BuildModeManager.GameMode.NORMAL_MODE
var _active_device := Device.KEYBOARD
var _last_joystick_input := Vector2.ZERO
var _virtual_cursor_position := Vector2.ZERO


func _ready():
	Events.build_mode_changed.connect(_on_build_mode_changed)

	set_process(false)


func _input(event):
	if event is InputEventMouseMotion:
		_active_device = Device.KEYBOARD
	elif event is InputEventJoypadMotion:
		_active_device = Device.GAMEPAD
		_set_joystick_direction()


func _process(delta):
	_update_virtual_cursor_position(delta)


func setup(player: Player, cart: Minecart):
	_player = player
	_cart = cart

	set_process(true)


func get_active_device() -> Device:
	return _active_device


func get_cursor_position() -> Vector2:
	if _active_device == Device.KEYBOARD:
		return get_global_mouse_position()

	elif _active_device == Device.GAMEPAD:
		if _build_mode == BuildModeManager.GameMode.NORMAL_MODE:
			return _player.global_position + (_get_joystick_direction() * 20)
		elif _build_mode == BuildModeManager.GameMode.BUILD_MODE:
			return _virtual_cursor_position

	return Vector2.ZERO


func get_cart_aim_position() -> Vector2:
	if _active_device == Device.KEYBOARD:
		return get_global_mouse_position()
	elif _active_device == Device.GAMEPAD:
		return _cart.global_position + (_get_joystick_direction() * 100)

	return Vector2.ZERO


func is_grabed_released(event: InputEvent) -> bool:
	if _active_device == Device.KEYBOARD:
		return event.is_action_released("grab")
	elif _active_device == Device.GAMEPAD:
		return Input.is_action_just_released("grab_trigger")

	return false


func is_grabed_pressed(event: InputEvent) -> bool:
	if _active_device == Device.KEYBOARD:
		return event.is_action_pressed("grab")
	elif _active_device == Device.GAMEPAD:
		return Input.is_action_just_pressed("grab_trigger")

	return false


func _get_joystick_direction() -> Vector2:
	return _last_joystick_input


func _set_joystick_direction() -> void:
	var joystick_input := Input.get_vector("look_left", "look_right", "look_up", "look_down")
	var joystick_deadzone := 0.8

	if joystick_input.length() < joystick_deadzone:
		return

	_last_joystick_input = joystick_input


func _update_virtual_cursor_position(delta: float) -> void:
	if _build_mode == BuildModeManager.GameMode.NORMAL_MODE:
		_virtual_cursor_position = _player.position
		return

	_virtual_cursor_position += (Input.get_vector("left", "right", "up", "down") * delta * 160)


func _on_build_mode_changed(mode: BuildModeManager.GameMode) -> void:
	_build_mode = mode
	# match mode:
	# 	BuildModeManager.GameMode.BUILD_MODE:
	# 		pass
	#
	# 	BuildModeManager.GameMode.NORMAL_MODE:
	# 		pass
