class_name TutorialStages
extends Node2D

const NEXT_STAGE_DELAY := 1.0
const DIALOGE_DURATION := 5.0

const KEYBINDING = {
	"move": ["[W],[A],[S],[D]", "(Left Stick)"],
	"sprint": ["[Shift]", "(A)"],
	"continue": ["[Space]", "(A)"],
	"fire": ["[Left Click]", "(Right Trigger) and (Right Stick)"],
	"bomb": ["[F]", "(X)"],
	"cart": ["[Right Click]", "(Left Trigger)"],
	"build_mode": ["[Tab]", "(Y)"],
	"place_building": ["[Left Click]", "(A)"],
	"smelter": ["[1]", "(Right Shoulder) till you select slot 1"],
	"powerplant": ["[2]", "(Right Shoulder) till you select slot 1"],
	"escape": ["[Esc]", "(Menu)"],
}

enum Stages {
	GREETINGS_1,
	GREETINGS_2,
	EXPLAIN_SCORE,
	MOVE,
	SPRINT,
	SHOT,
	PLACE_BOMB,
	PULL_CART,
	PUSH_CART,
	KILL_ENEMY,
	EXPLAIN_TRAPS,  # and there need ofr energy
	MINING,
	EXPLAIN_DRILL,
	COLLECT_ORE,
	EXPLAIN_CONVEYOR,
	BUILD_MODE,
	PLACE_SMELTER,
	EXPLAIN_DESTROY,
	PLACE_POWER_PLANT,
	PLACE_PIPE,
	FILL_SMELTER,
	REDUCE_HEAT,
	OUTRO_1,
	OUTRO_2,
}

enum TutorialEvents {
	GREETINGS_1_DONE,
	GREETINGS_2_DONE,
	EXPLAIN_SCORE_DONE,
	MOVEMENT_KEY_PRESSED,
	SPRINT_KEY_PRESSED,
	FIRE_PRESSED,
	BOMB_EXPLODED,
	CART_PULLED,
	CART_PUSHED,
	ENEMY_DIED,
	EXPLAIN_TRAPS_DONE,
	ORE_EXTRACTED,
	EXPLAIN_DRILL_DONE,
	ORE_COLLECTED,
	EXPLAIN_CONVEYOR_DONE,
	BUILD_MODE_ACTIVATE,
	SMELTER_PLACED,
	EXPLAIN_DESTROY_DONE,
	POWER_PLANT_PLACED,
	SMELTER_CONNECTED,
	SMELTER_LOADED,
	HEAT_REDUCED,
	OUTRO_1_DONE,
	NONE,
}

signal stage_started(stage: Stages)
signal tutorial_event(event: TutorialEvents)

var _tutorial_gui: TutorialGUI
var _entity_tracker: EntityTracker
var _player: Player
var _cart: Minecart
var _drag_objects: DragObjects
var _event_to_wait_for := TutorialEvents.NONE
var _current_stage := 0
var _wait_for_stage := false


func _ready():
	tutorial_event.connect(_on_tutorial_event)
	Events.bomb_explode.connect(_on_bomb_explode)
	Events.build_mode_changed.connect(_on_build_mode_changed)
	InputManager.device_changed.connect(_on_device_changed)


func _unhandled_input(event: InputEvent):
	if event.is_action_pressed("continue"):
		if _tutorial_gui.text_full_visable():
			match _event_to_wait_for:
				TutorialEvents.GREETINGS_1_DONE:
					_next_stage(0.2)
				TutorialEvents.GREETINGS_2_DONE:
					_next_stage(0.2)
				TutorialEvents.EXPLAIN_TRAPS_DONE:
					_next_stage(0.2)
				TutorialEvents.EXPLAIN_SCORE_DONE:
					_next_stage(0.2)
				TutorialEvents.EXPLAIN_DRILL_DONE:
					_next_stage(0.2)
				TutorialEvents.EXPLAIN_CONVEYOR_DONE:
					_next_stage(0.2)
				TutorialEvents.EXPLAIN_DESTROY_DONE:
					_next_stage(0.2)
				TutorialEvents.OUTRO_1_DONE:
					_next_stage(0.2)
		else:
			_tutorial_gui.text_finished()


func _input(event: InputEvent):
	if event.is_action_pressed("fire"):
		tutorial_event.emit(TutorialEvents.FIRE_PRESSED)
	if event.is_action_pressed("boost"):
		tutorial_event.emit(TutorialEvents.SPRINT_KEY_PRESSED)


func _process(_delta):
	if _player.velocity.length() > 0.0:
		tutorial_event.emit(TutorialEvents.MOVEMENT_KEY_PRESSED)

	if _cart.resources.ore_amount > 0:
		tutorial_event.emit(TutorialEvents.ORE_COLLECTED)

	if _drag_objects.is_graped:
		tutorial_event.emit(TutorialEvents.CART_PULLED)

	# this is also true before the cart is pulled, but because the pulled state comes before the push state its fine
	if not _drag_objects.is_graped:
		tutorial_event.emit(TutorialEvents.CART_PUSHED)

	if "Smelter" in _entity_tracker.blocked_cells.values():
		tutorial_event.emit(TutorialEvents.SMELTER_PLACED)

	if "PowerPlant" in _entity_tracker.blocked_cells.values():
		tutorial_event.emit(TutorialEvents.POWER_PLANT_PLACED)


func setup(
	tutorial_gui: TutorialGUI,
	entity_tracker: EntityTracker,
	pipe_system: PipeSystem,
	player: Player,
	cart: Minecart
) -> void:
	_tutorial_gui = tutorial_gui
	_entity_tracker = entity_tracker
	_player = player
	_cart = cart
	_drag_objects = player.get_node("DragObjects")

	pipe_system._paths.paths_changed.connect(_on_pipe_path_changed)
	_start_stage()


func _on_tutorial_event(event: TutorialEvents) -> void:
	if not _tutorial_gui.text_full_visable():
		return
	if _event_to_wait_for == event:
		_next_stage(NEXT_STAGE_DELAY)


func _on_bomb_explode(_pos: Vector2) -> void:
	tutorial_event.emit(TutorialEvents.BOMB_EXPLODED)


func _on_build_mode_changed(mode: BuildModeManager.GameMode) -> void:
	if mode == BuildModeManager.GameMode.BUILD_MODE:
		tutorial_event.emit(TutorialEvents.BUILD_MODE_ACTIVATE)


func _on_pipe_path_changed() -> void:
	tutorial_event.emit(TutorialEvents.SMELTER_CONNECTED)


func _on_device_changed(_device: InputManager.Device):
	_tutorial_gui.update_text(_get_current_stage_text())


func _next_stage(delay: float) -> void:
	if _wait_for_stage:
		return

	_wait_for_stage = true
	await get_tree().create_timer(delay).timeout
	_wait_for_stage = false

	_current_stage += 1
	_start_stage()


func _start_stage() -> void:
	stage_started.emit(_current_stage as Stages)

	_event_to_wait_for = _current_stage as TutorialEvents
	_tutorial_gui.show_text(_get_current_stage_text(), DIALOGE_DURATION)


func _get_key_bind(action: String) -> String:
	return KEYBINDING[action][InputManager.get_active_device()]


func _get_current_stage_text() -> String:
	var text = ""
	match _current_stage as Stages:
		Stages.GREETINGS_1:
			text += "Alright, Spaceman! Listen up! I ain't here to hold your hand through the cosmos! As part of the best intergalactic mining troop you already know the drill."
			text += "\n[i](Press %s to continue)[/i]" % _get_key_bind("continue")

		Stages.GREETINGS_2:
			text += "Before we send you to some godforsaken planet you have complete this basic check. So let's get this over with!"
			text += "\n[i](Press %s to continue)[/i]" % _get_key_bind("continue")

		Stages.EXPLAIN_SCORE:
			text += "Top left corner of your HUD, troopers! That's where you see how much energy you've pumped out and how much cash you've got. More energy, more money, more power! Keep producing, or we're done!"
			text += "\n[i](Press %s to continue)[/i]" % _get_key_bind("continue")

		Stages.MOVE:
			text += "Spaceman, on your feet! Move like your life depends on it, because out here, it does! Hustle, hustle, hustle!"
			text += "\n[i](Press %s to move)[/i]" % _get_key_bind("move")

		Stages.SPRINT:
			text += "Need to move faster, slowpokes? Fire up those spacesuit rockets and blast across the battlefield! No excuses, MOVE!"
			text += "\n[i](Press %s to use boost)[/i]" % _get_key_bind("sprint")

		Stages.SHOT:
			text += "Your blaster's your best friend, spaceman! Treat it like your girlfriend. Keep it close and use it well!!"
			text += "\n[i](Press %s to shot)[/i]" % _get_key_bind("fire")

		Stages.PLACE_BOMB:
			text += "Got a big enemy group? Bombs away! But don't be stupid, blow them up, not yourself!"
			text += "\n[i](Press %s to place a bomb[/i])" % _get_key_bind("bomb")

		Stages.KILL_ENEMY:
			text += "Enemies are just ore with legs! Kill 'em all! That's why we're here! Show no mercy, for it won't show you any!"

		Stages.EXPLAIN_TRAPS:
			text += "Traps and turret - your best friends in killing faster! But remember, they need power! If your power plants are not producing, these are just expensive paperweights!"
			text += "\n[i](Press %s to continue)[/i]" % _get_key_bind("continue")

		Stages.MINING:
			text += "Get those pickaxes swinging! Dig deep and hard, we need every ounce of ore you can find!\n[i](Pickaxe will be used automatically when standing close to an enemy)[/i]"

		Stages.EXPLAIN_DRILL:
			text += "The pickaxe ain't your only tool for getting that ore. Place drills near enemy corpses, and they'll automatically deconstruct those bodies into ore for you. No need to get your hands dirty!"
			text += "\n[i](Press %s to continue)[/i]" % _get_key_bind("continue")

		Stages.PULL_CART:
			text += "Pull that cart like it's your favorite pet! Remote control, it's not rocket science, spaceman! Use it "
			text += "\n[i](Press %s to pull the cart towards you)[/i]" % _get_key_bind("cart")

		Stages.PUSH_CART:
			text += "Send those carts where they need to go! Remote control, aim true, and keep 'em rolling!"
			text += (
				"\n[i](Press and hold %s to push the cart towards your cursor)[/i]"
				% _get_key_bind("cart")
			)

		Stages.COLLECT_ORE:
			text += "Ore on the ground! Scoop it up fast, spaceman! Don't leave a single nugget behind!\n[i](Move the carts over the ore to collect it)[/i]"

		Stages.EXPLAIN_CONVEYOR:
			text += "Quit dragging stuff around like cavemen! There's a more efficient way - conveyor belts! Use 'em to transport ore and corpses automatically. Let the belts do the heavy lifting while you focus on the fight!"
			text += "\n[i](Press %s to continue)[/i]" % _get_key_bind("continue")

		Stages.BUILD_MODE:
			text += "Switch to build mode, spaceman! Plan and place those buildings like you're building a space palace!"
			text += "\n[i](Press %s to enter build mode)[/i]" % _get_key_bind("build_mode")

		Stages.PLACE_SMELTER:
			text += "Smelters up, spaceman! Place them solid and secure. Fill 'em with ore and turn up the heat! Make those smelters roar!"
			text += (
				"\n[i](Press %s to select the smelter in the quickbar and place it with %s)][/i]"
				% [_get_key_bind("smelter"), _get_key_bind("place_building")]
			)

		Stages.EXPLAIN_DESTROY:
			text += "Got a useless building? BLOW IT UP! We don't need junk clogging up our battlefield. Clear it out and keep things tight!"
			text += "\n[i](Press %s to continue)[/i]" % _get_key_bind("continue")

		Stages.PLACE_POWER_PLANT:
			text += "Power plants are next! They turn heat into energy! Set 'em up right and keep 'em running hot!"
			text += (
				"\n[i](Press %s to select the power plant in the quickbar and place it with %s)[/n]"
				% [_get_key_bind("powerplant"), _get_key_bind("place_building")]
			)

		Stages.PLACE_PIPE:
			text += "Pipes, spaceman! Connect smelters to power plants, and let that heat flow like lava!"
			text += (
				"\n[i](Press and hold %s on the smelter, drag the cursor to the power plant, and release)[/i]"
				% _get_key_bind("place_building")
			)

		Stages.FILL_SMELTER:
			text += "Get those carts to the smelters, unload the ore, and keep those furnaces blazing hot!"
			text += (
				"\n[i](Press %s to exit build mode and move the cart close to the smelter will fill it with ore)[/i]"
				% _get_key_bind("build_mode")
			)

		Stages.REDUCE_HEAT:
			text += "Smelters overheating? That's pressure building up! Release it manually by standing next to it or your smelters will stop! Keep that production flowing!"
			text += "\n[i](Press %s to continue)[/i]" % _get_key_bind("continue")

		Stages.OUTRO_1:
			text += "Alright, spaceman! This energy powers our turrets and traps. Keep the flow steady, and we'll crush the enemy like bugs!"
			text += "\n[i](Press %s to continue)[/i]" % _get_key_bind("continue")

		Stages.OUTRO_2:
			text += "Congratulation, spaceman! Get out there and get it done, because in space, it's them or us. Move out, spaceman!"
			text += (
				"\n[i](Press %s and click 'Quit' to exit the tutorial)[/i]"
				% _get_key_bind("escape")
			)

	return text
