class_name TutorialGUI
extends MarginContainer

@onready var _animation: AnimatedSprite2D = $HBoxContainer/MarginContainer/GeneralAnimationSprite
@onready var _text: RichTextLabel = $HBoxContainer/PanelContainer/MarginContainer/GeneralTextBox
@onready var _container: Control = $HBoxContainer

var _tween: Tween


func _ready():
	_container.hide()

	Events.pause_menu_shown.connect(_on_pause_menu_shown)


func show_text(text: String, duration: float) -> void:
	get_tree().paused = true

	_text.text = text
	_text.visible_ratio = 0.0

	_tween = get_tree().create_tween()
	_tween.set_pause_mode(Tween.TWEEN_PAUSE_PROCESS)
	_tween.tween_property(_text, "visible_ratio", 1.0, duration)
	_tween.tween_callback(text_finished)

	_animation.play("speak")

	_container.show()


func update_text(text: String) -> void:
	_text.text = text


func hide_text() -> void:
	_container.hide()


func text_full_visable() -> bool:
	return _text.visible_ratio >= 1.0


func text_finished() -> void:
	if _tween:
		_tween.kill()

	_animation.play("idle")

	_text.visible_ratio = 1.0

	get_tree().paused = false


func _on_pause_menu_shown(is_shown: bool) -> void:
	visible = not is_shown
