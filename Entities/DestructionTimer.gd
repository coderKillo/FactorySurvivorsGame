class_name DestructionTimer
extends Timer

signal finish_destruction(cellv: Vector2i)

@export var destruction_time: float = 0.5

@onready var _progress_bar: ProgressBar = $ProgressBar

var _current_destruction_location: Vector2i


func _ready():
	_progress_bar.max_value = destruction_time
	_progress_bar.hide()


func _process(_delta):
	if time_left > 0:
		_progress_bar.show()
		_progress_bar.value = time_left
		_progress_bar.global_position = _progress_bar.get_global_mouse_position()
	else:
		_progress_bar.hide()


func start_destruction(location: Vector2i):
	timeout.connect(_finish_destruction.bind(location), CONNECT_ONE_SHOT)
	start(destruction_time)
	_current_destruction_location = location


func update_input(event: InputEvent, cell_under_mouse: Vector2i):
	if event is InputEventMouseButton:
		_about_destruction()
	if event is InputEventMouseMotion and cell_under_mouse != _current_destruction_location:
		_about_destruction()


func _about_destruction():
	if timeout.is_connected(_finish_destruction):
		timeout.disconnect(_finish_destruction)

	stop()
	_current_destruction_location = Vector2i()


func _finish_destruction(location: Vector2i):
	finish_destruction.emit(location)
