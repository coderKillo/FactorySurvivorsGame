class_name Enemy
extends CharacterBody2D

@export var speed = 20
@export var attack_range = 8
@export var target: Node2D = null

var color := ""
var max_health := 20
var damage := 10
var parts := []
var need_target_update := false

enum States { IDLE, MOVE, ATTACK, DEATH }

var _current_state: States = States.IDLE

@onready var model: ModelComponent = $ModelComponent
@onready var health: Health = $Health
@onready var weapon: EnemyWeapon = $Weapon

@onready var _hurt_box: HurtBoxComponent = $HurtBoxComponent
@onready var _raycast: RayCast2D = $RayCast2D
@onready var _navigation_timer: Timer = $UpdateNavigationTimer

var _is_attacking := false
var _is_death := false
var _is_pyhsics_called_after_death := false
var _target_position := Vector2.ZERO

var _query_parameters := NavigationPathQueryParameters2D.new()
var _query_result := NavigationPathQueryResult2D.new()
var _path: PackedVector2Array = []


func _ready():
	health.max_health = max_health
	weapon.damage = damage
	parts.reverse()
	for part in parts:
		model.add_part(part)

	health.death.connect(_on_death)
	_hurt_box.hit.connect(_on_hit)
	_hurt_box.push_back.connect(_on_hit_push_back)

	_navigation_timer.timeout.connect(_on_update_target_position)


func _process(_delta):
	if _is_attacking or _is_death:
		return

	if _current_state == States.DEATH:
		pass
	elif target == null:
		_current_state = States.IDLE
	elif _target_in_range():
		_current_state = States.ATTACK
	else:
		_current_state = States.MOVE

	_handle_states()


func _physics_process(delta):
	if _is_pyhsics_called_after_death:
		return

	# execute physics only once on death (to apply pushback)
	if _current_state == States.DEATH:
		_is_pyhsics_called_after_death = true

	var direction = Vector2.ZERO

	if _current_state == States.MOVE:
		update_path_target(delta)
		direction = global_position.direction_to(_get_target_position())

	velocity = direction * speed

	move_and_slide()


func _handle_states() -> void:
	if _current_state == States.IDLE:
		pass

	elif _current_state == States.MOVE:
		_face_target()

		model.play(EnemySpriteFrames.WALK)

	elif _current_state == States.ATTACK:
		_face_target()

		_is_attacking = true

		model.play(EnemySpriteFrames.ATTACK)

		await get_tree().create_timer(0.2).timeout

		weapon.fire()

		await model.animation_looped

		_is_attacking = false

	elif _current_state == States.DEATH:
		_is_death = true

		model.play(EnemySpriteFrames.DEATH)
		await model.animation_finished

		Events.enemy_died.emit(self)
		queue_free()


func _face_target():
	var direction = position.direction_to(target.position)

	weapon.look_at(target.global_position + Vector2(0, -3))

	if direction.x > 0:
		model.flip_h = false
		weapon.scale.x = -1
	elif direction.x < 0:
		model.flip_h = true
		weapon.scale.x = 1
		weapon.rotation_degrees += 180


func _target_in_range() -> bool:
	return target != null and global_position.distance_to(target.global_position) < attack_range


func _get_target_position() -> Vector2:
	if target == null:
		return global_position + velocity

	return _target_position


func update_path_target(delta):
	if _path.is_empty():
		_target_position = target.global_position
		return

	if global_position.distance_to(_target_position) > (speed * delta):
		return

	_path.remove_at(0)

	if not _path.is_empty():
		_target_position = _path[0]


func _on_death():
	_current_state = States.DEATH


func _on_hit(_damage: int) -> void:
	model.hit(0.2)


func _on_update_target_position() -> void:
	if target == null:
		return

	var target_pos := target.global_position

	_raycast.target_position = _raycast.global_position.direction_to(target_pos) * 10
	if _raycast.is_colliding():
		_query_parameters.map = get_world_2d().get_navigation_map()
		_query_parameters.start_position = global_position
		_query_parameters.target_position = target_pos
		_query_parameters.path_postprocessing = (
			NavigationPathQueryParameters2D.PATH_POSTPROCESSING_EDGECENTERED
		)
		_query_parameters.navigation_layers = 1

		NavigationServer2D.query_path(_query_parameters, _query_result)
		_path = _query_result.get_path()
		if _path.size() >= 2:
			_path.remove_at(0)
			_target_position = _path[0]

	else:
		_path = []


func _on_hit_push_back(direction: Vector2) -> void:
	velocity = direction * 200
