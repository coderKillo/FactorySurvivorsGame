class_name ModelComponent
extends Node2D

signal animation_finished
signal animation_looped
signal frame_changed

var animation: String = "":
	get = _get_animation
var flip_h: bool = false:
	set = _set_flip_h

var _parts: Array[AnimatedSprite2D]


func add_part(anim: AnimatedSprite2D) -> void:
	if _parts.is_empty():
		anim.animation_finished.connect(_on_animation_finished)
		anim.animation_looped.connect(_on_animation_looped)
		anim.frame_changed.connect(_on_frame_changed)

	_parts.append(anim)
	add_child(anim)


func play(anim_name: String) -> void:
	for part in _parts:
		part.play(anim_name)


func get_death_sprite() -> Array[Sprite2D]:
	var sprites: Array[Sprite2D] = []

	for part in _parts:
		var sprite = Sprite2D.new()
		sprite.position = position
		sprite.texture = part.sprite_frames.get_frame_texture(EnemySpriteFrames.DEATH, 2)
		sprite.modulate = part.modulate
		sprite.material = part.material
		sprite.flip_h = part.flip_h
		sprites.append(sprite)

	return sprites


func _on_animation_finished():
	animation_finished.emit()


func _on_animation_looped():
	animation_looped.emit()


func _on_frame_changed():
	frame_changed.emit()


func _set_flip_h(value):
	for part in _parts:
		part.flip_h = value


func _get_animation() -> String:
	for part in _parts:
		return part.animation
	return ""
