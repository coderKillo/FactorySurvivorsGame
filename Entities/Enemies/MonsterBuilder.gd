class_name MonsterBuilder
extends RefCounted

const PARTS_PATH = "res://Entities/Enemies/Parts"

const DIFFICULTY_DAMAGE_FACTOR := 0.1
const DIFFICULTY_HEALTH_FACTOR := 0.2

const BASE_MONSTER_HEALTH := 12
const BASE_MONSTER_DAMAGE := 3
const BASE_MONSTER_SPEED := 20

const CRYSTAL_SCALE_FACTOR := 2

var BaseEnemy := preload("res://Entities/Enemies/BaseEnemy.tscn")

var _parts: Dictionary = {}
var _current_level := 1
var _monster_difficulty_array := [1, 1, 1, 1, 1]

var _monster_color = ["blue", "green", "yellow", "brown", "purple", "red", "grey"]

var _stats_table = {
	"head1": PartStats.new(-5, -2, 5, 2, 10),
	"head2": PartStats.new(0, 0, 0, 3, 3),
	"head3": PartStats.new(5, 2, -5, 4, 1),
	"body1": PartStats.new(0, 0, 5, 2, 5),
	"body2": PartStats.new(5, 2, -10, 4, 1),
	"arms": PartStats.new(2, 5, -5, 4, 1),
	"no_arms": PartStats.new(0, 0, 0, 0, 3),
	"legs1": PartStats.new(0, 0, 0, 2, 10),
	"legs2": PartStats.new(0, -2, 10, 4, 1),
	"tail1": PartStats.new(-2, -1, 0, 2, 5),
	"tail2": PartStats.new(5, 0, -5, 4, 1),
	"tail3": PartStats.new(2, -1, 5, 3, 3),
	"no_tail": PartStats.new(0, 0, 0, 0, 10),
	"wings1": PartStats.new(0, 0, 10, 2, 4),
	"wings2": PartStats.new(2, 3, 5, 4, 1),
	"no_wings": PartStats.new(0, 0, 0, 0, 15),
	"crystal1": PartStats.new(20, 0, -20, 0, 1),
	"no_crystal": PartStats.new(0, 0, 0, 0, 50),
}


class PartStats:
	func _init(_health, _damage, _speed, _credits, _probability):
		self.health = _health
		self.damage = _damage
		self.speed = _speed
		self.credits = _credits
		self.probability = _probability

	var health := 0
	var damage := 0
	var speed := 0
	var credits := 0
	var probability := 0


func _init():
	_load_parts()

	Events.leveled_up.connect(_on_level_up)


func create_monster_wave(credits: int) -> Array[Enemy]:
	var enemies: Array[Enemy] = []

	while credits > 0:
		var difficulty_level := _monster_difficulty_array.pick_random() as int
		var color := _monster_color[difficulty_level % _monster_color.size()] as String
		var size_factor := ceil(float(difficulty_level) / _monster_color.size()) as float

		var enemy := BaseEnemy.instantiate() as Enemy
		var used_budget = _create_monster(enemy, color)

		enemy.max_health = int(enemy.max_health * (1 + DIFFICULTY_HEALTH_FACTOR * difficulty_level))
		enemy.damage = int(enemy.damage * (1 + DIFFICULTY_DAMAGE_FACTOR * difficulty_level))

		enemy.scale *= size_factor

		used_budget *= difficulty_level
		credits -= used_budget

		enemies.append(enemy)

	return enemies


func _create_monster(enemy: Enemy, color: String) -> int:
	var credits_used := 0
	var part := ""

	enemy.max_health = BASE_MONSTER_HEALTH
	enemy.damage = BASE_MONSTER_DAMAGE
	enemy.speed = BASE_MONSTER_SPEED
	enemy.color = color

	part = _pick_random_part(["head1", "head2", "head3"])
	credits_used += _add_part(enemy, part, enemy.color)

	part = _pick_random_part(["body1", "body2"])
	credits_used += _add_part(enemy, part, enemy.color)

	part = _pick_random_part(["arms", "no_arms"])
	if part != "no_arms":
		credits_used += _add_part(enemy, part, enemy.color)

	part = _pick_random_part(["legs1", "legs2", "tail1", "tail3"])
	credits_used += _add_part(enemy, part, enemy.color)

	if part == "tail1":
		# flying
		part = _pick_random_part(["wings1", "wings2"])
		credits_used += _add_part(enemy, part, _monster_color.pick_random())

	else:
		# ground
		part = _pick_random_part(["wings1", "wings2", "no_wings"])
		if part != "no_wings":
			credits_used += _add_part(enemy, part, _monster_color.pick_random())

		part = _pick_random_part(["tail2", "no_tail"])
		if part != "no_tail":
			credits_used += _add_part(enemy, part, enemy.color)

	part = _pick_random_part(["crystal1", "no_crystal"])
	if part != "no_crystal":
		credits_used += _add_part(enemy, part, _monster_color.pick_random())
		enemy.scale *= CRYSTAL_SCALE_FACTOR

	return credits_used


func _add_part(enemy: Enemy, part_name: String, color: String) -> int:
	var part := _parts[part_name].instantiate() as AnimatedSprite2D
	part.material = RecolorTable.create_recolor_material_from(color)
	enemy.parts.append(part)

	var stat = _stats_table[part_name] as PartStats
	enemy.damage += stat.damage
	enemy.max_health += stat.health
	if enemy.max_health < 2:
		enemy.max_health = 2
	enemy.speed += stat.speed
	if enemy.speed < 5:
		enemy.speed = 5

	return stat.credits


func _pick_random_part(parts: Array[String]) -> String:
	var _distribution: Array[String] = []

	for part in parts:
		var stat := _stats_table[part] as PartStats
		for i in stat.probability:
			_distribution.append(part)

	return _distribution.pick_random()


func _on_level_up() -> void:
	_current_level += 1

	if _current_level > _monster_difficulty_array.size():
		_monster_difficulty_array[_current_level % _monster_difficulty_array.size()] += 1


func _load_parts() -> void:
	var dir = DirAccess.open(PARTS_PATH)
	if not dir:
		print("Error occured when try to access: ", PARTS_PATH)
		return

	dir.list_dir_begin()

	var filename := dir.get_next()
	while filename != "":
		if filename.begins_with("part_"):
			if filename.ends_with(".remap"):
				filename = filename.trim_suffix(".remap")
			var key = filename.replace("part_", "").replace(".tscn", "")
			_parts[key] = load(dir.get_current_dir() + "/" + filename)

		filename = dir.get_next()
