class_name Player
extends CharacterBody2D

const MID_LOAD_TRESHOLD = 0.5
const HIGH_LOAD_TRESHOLD = 0.8
const BOOST = 3.0
const BOOST_COST_PER_SEC = 10.0

@export var movement_speed = 300.0

@onready var health: Health = $Health
@onready var energy: Energy = $Energy

@onready var _animations: Array[AnimatedSprite2D] = [$Model/Body, $Model/Hands]
@onready var _weapons: Array[Weapon] = [$Weapons/PickAxe, $Weapons/Blaster]
@onready var _weapon_timer: Timer = $Weapons/Cooldown
@onready var _drag_objects: DragObjects = $DragObjects
@onready var _auto_attack_area: Area2D = $AutoattackArea
@onready var _bomb_placer: BombPlacer = $BombPlacer
@onready var _selector: BuildingSelector = $BuildingSelector
@onready var _wrench: AnimatedSprite2D = $WrenchAnimation

var load_treshold_slow := 15

var _boost_active := false


func _ready():
	_weapons[1].energy_used.connect(_on_weapon_energy_used)
	Events.upgrade_data_changed.connect(_on_upgrade_data_changed)
	Events.system_tick.connect(_on_system_tick)
	health.death.connect(_on_death)

	_on_upgrade_data_changed()


func _process(_delta):
	if _weapon_timer.time_left > 0:
		_animations[1].hide()
	else:
		_animations[1].show()


func _physics_process(delta):
	fire(0, _auto_attack_area_has_entities())

	var input_direction = Input.get_vector("left", "right", "up", "down")

	_set_animation(input_direction)

	var speed = movement_speed

	if _is_boosting():
		energy.energy -= BOOST_COST_PER_SEC * delta
		speed *= BOOST

	velocity = input_direction * speed

	move_and_slide()


func _unhandled_input(event):
	if event.is_action_pressed("boost"):
		_boost_active = true
	if event.is_action_released("boost"):
		_boost_active = false

	if _drag_objects.bodies_grabed():
		if InputManager.is_grabed_pressed(event):
			_drag_objects.charge()
		if InputManager.is_grabed_released(event) and _drag_objects.is_charging:
			_drag_objects.release()
	else:
		if InputManager.is_grabed_pressed(event):
			_drag_objects.grab()


func fire(slot: int, pressed: bool) -> void:
	if slot == 1 and energy.energy <= 0:
		return

	_weapons[slot].fire(pressed)


func place_bomb() -> void:
	if energy.energy <= _bomb_placer.energy_cost:
		return

	_on_weapon_energy_used(_bomb_placer.energy_cost)
	_bomb_placer.place()


func _on_weapon_energy_used(amount):
	energy.energy -= amount

	if energy.energy <= 0:
		_weapons[1].fire(false)


func _on_upgrade_data_changed() -> void:
	health.max_health = UpgradeData.player_data.max_health
	energy.max_energy = UpgradeData.player_data.max_energy


func _on_death() -> void:
	Events.player_died.emit()

	hide()


func _on_system_tick(delta: float) -> void:
	if _selector.selected == null:
		_wrench.hide()
		return

	var entity := _selector.selected as Entity
	var entity_name := Library.get_entity_name(entity)

	if entity_name == "Smelter":
		var heat_bucket = entity.get_node_or_null("HeatBucket") as Bucket
		heat_bucket.take(int(100 * delta))

		_wrench.show()
		_wrench.global_position = entity.global_position
	else:
		_wrench.hide()


func _is_boosting() -> bool:
	return _boost_active and energy.energy > 0.0


func _set_animation(direction: Vector2):
	for animation in _animations:
		if _is_boosting():
			animation.play("boost")
		elif direction:
			animation.play("walk")
		else:
			animation.play("idle")

		animation.flip_h = _weapons[0].is_right


func _auto_attack_area_has_entities() -> bool:
	var areas := []
	for area in _auto_attack_area.get_overlapping_areas():
		var node := area as Node2D
		var entity := node as GroundEntity
		if entity != null and not entity.is_destructable:
			continue

		areas.append(area)

	return not areas.is_empty()
