extends Camera2D

@onready var _timer: Timer = $Timer

@export var amplitude := 6.0
@export var duration := 0.8
@export var damp_easing := 1.0

@export var _shake := false:
	set = _set_shake


func _ready():
	Events.camera_shake.connect(_on_camera_shake)
	Events.player_died.connect(_on_player_death)
	_timer.timeout.connect(_on_timer_timeout)
	_timer.wait_time = duration

	randomize()
	set_process(false)


func _process(_delta):
	var damping := ease(_timer.time_left / _timer.wait_time, damp_easing)
	offset = Vector2(
		randf_range(amplitude, -amplitude) * damping, randf_range(amplitude, -amplitude) * damping
	)


func _on_camera_shake(amount: float) -> void:
	amplitude = amount
	_shake = true


func _on_timer_timeout() -> void:
	_shake = false


func _set_shake(value: bool) -> void:
	_shake = value
	set_process(value)
	offset = Vector2.ZERO
	if _shake:
		_timer.start()


func _set_duration(value: float) -> void:
	duration = value
	_timer.wait_time = value


func _on_player_death() -> void:
	offset = Vector2.ZERO
	position_smoothing_enabled = false

	var tween = get_tree().create_tween()
	tween.set_pause_mode(Tween.TWEEN_PAUSE_PROCESS)

	tween.tween_property(self, "zoom", Vector2(2.0, 2.0), 1.0)
