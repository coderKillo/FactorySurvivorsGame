extends Node2D

var damage = 20
var delay = 3  # in sec

@onready var _hit_area: Area2D = $Area2D
@onready var _animation: AnimatedSprite2D = $AnimatedSprite2D

var _is_charged := false
var _is_exploded := false


func _ready():
	_charge()


func _physics_process(_delta):
	if not _is_charged or _is_exploded:
		return

	_explode()


func _charge() -> void:
	_animation.play("charge")

	await _animation.animation_finished

	_is_charged = true


func _explode() -> void:
	_is_exploded = true

	_damage()

	Events.camera_shake.emit(8.0)
	SoundManager.play("explosive")

	_animation.play("explode")

	await _animation.animation_finished

	queue_free()


func _damage() -> void:
	for body in _hit_area.get_overlapping_bodies():
		var destruction := body.get_node_or_null("DestructionComponent") as DestructionComponent
		if destruction != null:
			destruction.destruct(damage)

	for area in _hit_area.get_overlapping_areas():
		var hurt_box = area as HurtBoxComponent
		if hurt_box != null:
			hurt_box.take_damage(damage, global_position)
