class_name Energy
extends Node2D

signal update_energy(amount, max_value)
signal charge_status_changed(is_charging)

@export var max_energy := 100
@export var energy_low_trashold := 20
@export var charge_amount := 2  # charge per system tick
@export var charge_delay := 1

@onready var energy := 0:
	set = _set_energy
@onready var _timer: Timer = $Timer

var _is_charging: bool = false:
	set(value):
		_is_charging = value
		charge_status_changed.emit(value)


func _ready():
	energy = max_energy
	Events.system_tick.connect(_on_system_tick)
	_timer.timeout.connect(_on_charing_timer_timeout)


func is_charging() -> bool:
	return _is_charging


func charge_is_low() -> bool:
	return energy <= energy_low_trashold


func _set_energy(value):
	if value < energy:
		_start_charging_timer()

	energy = value
	update_energy.emit(energy, max_energy)


func _start_charging_timer() -> void:
	_is_charging = false
	_timer.start(charge_delay)


func _on_charing_timer_timeout() -> void:
	_is_charging = true


func _on_system_tick(_delta):
	if not is_charging():
		return

	energy = clamp(energy + charge_amount, 0, max_energy)
