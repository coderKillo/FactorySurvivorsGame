class_name BuildingSelector
extends Area2D

@onready var _marker: Node2D = $Marker

var selected: Entity


func _physics_process(_delta) -> void:
	selected = _find_closest_entity()
	_set_marker()


func _find_closest_entity() -> Entity:
	var distance := 0.0
	var closest_entity: Entity
	for body in get_overlapping_bodies():
		var entity := body as Entity
		if entity == null:
			continue
		if closest_entity == null or distance > global_position.distance_to(entity.global_position):
			closest_entity = entity
			distance = global_position.distance_to(entity.global_position)

	return closest_entity


func _set_marker() -> void:
	if selected != null:
		_marker.show()
		_marker.global_position = selected.global_position
	else:
		_marker.hide()
