class_name DragObjects
extends Area2D

signal charge_time_updated(time: float)

@export var min_distance = 25
@export var max_charge_time := 1.0
@export var max_charge_push := 200
@export var pull_force := 40

@onready var _pull_line: Line2D = $PullLine
@onready var _push_line: Line2D = $PushLine

var is_charging := false
var is_graped := false

var _charge_time := 0.0:
	set(value):
		_charge_time = value
		charge_time_updated.emit(_charge_time)


func grab():
	is_graped = true

	SoundManager.play("cart_follow")


func charge():
	is_charging = true

	SoundManager.play("cart_carge")


func release():
	is_charging = false

	_push_bodies_in_mouse_direction()

	is_graped = false

	SoundManager.stop("cart_carge")
	SoundManager.play("cart_release")


func bodies_grabed() -> int:
	return len(get_bodies())


func _process(delta):
	if is_charging:
		_charge_time += delta
	else:
		_charge_time = 0.0


func _physics_process(_delta):
	_push_line.points = []
	_pull_line.points = []

	for body in get_bodies():
		var distance = body.global_position.distance_to(global_position)
		var direction = body.global_position.direction_to(global_position)
		var entity := body as GroundEntity

		if not entity.is_draggable:
			continue

		if distance > min_distance:
			entity.velocity = direction * pull_force

		var body_position = to_local(body.global_position)
		_pull_line.points = [body_position, position]

		if is_charging:
			var mouse_position = to_local(InputManager.get_cart_aim_position())
			_push_line.points = [body_position, mouse_position]


func get_bodies() -> Array:
	if not is_graped:
		return []
	var bodies = get_tree().get_nodes_in_group("draggable")
	bodies.sort_custom(
		func(a, b): return position.distance_to(a.position) > position.distance_to(b.position)
	)
	return bodies.slice(0, UpgradeData.player_data.max_bodies)


func _push_bodies_in_mouse_direction() -> void:
	for body in get_bodies():
		var entity := body as GroundEntity
		var direction = body.global_position.direction_to(InputManager.get_cart_aim_position())
		var charge_factor = min(_charge_time, max_charge_time) / max_charge_time

		if not entity.is_draggable:
			continue

		entity.add_force(max_charge_push * direction * charge_factor)
