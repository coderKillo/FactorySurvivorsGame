extends Entity

@export var shape: Shape2D
@export var shape_offset: Vector2

@onready var _animation: AnimatedSprite2D = $AnimatedSprite2D
@onready var _marker: Marker2D = $Marker2D

var _bodies: Array[PhysicsBody2D] = []


func _ready():
	Events.system_tick.connect(_on_system_tick)


func _physics_process(delta):
	var query := PhysicsShapeQueryParameters2D.new()
	query.set_shape(shape)
	query.collide_with_areas = true
	query.collision_mask = (1 + 2 + 4 + 1024)
	query.transform = global_transform

	var results := get_world_2d().direct_space_state.intersect_shape(query, 100)
	for result in results:
		var collider := result["collider"] as Node2D

		var origin = collider.global_position
		var destination = origin.move_toward(_marker.global_position, self.data.speed * delta)
		var motion = destination - origin

		var body := collider as CharacterBody2D
		if body:
			body.move_and_collide(motion)

		if collider.is_in_group("ground_entity"):
			collider.velocity = motion / delta


func _on_body_exited(body):
	_bodies.erase(body)


func _on_body_entered(body):
	_bodies.append(body)


func _sync_animation():
	var time_sek = Time.get_ticks_msec() / 1000.0
	var frame_duration = 0.1
	var total_duration = 16 * frame_duration
	var time_in_animation = fmod(time_sek, total_duration)

	_animation.stop()
	_animation.frame = floor(time_in_animation / frame_duration)


func _on_system_tick(_delta):
	_sync_animation()
