class_name TrapEntity
extends Entity

const ANIMATION_TIME = 1.0

@export var _sound_effect: String = ""

@onready var _animation: AnimatedSprite2D = $AnimatedSprite2D
@onready var _area: Area2D = $Area2D
@onready var _power: PowerReceiver = $PowerReceiver

var _active := false
var _effects: Array[TrapEffect]


func _ready():
	_power.received_power.connect(_on_received_power)
	_animation.speed_scale = ANIMATION_TIME / self.data.speed

	for child in $Effects.get_children():
		var effect := child as TrapEffect
		if effect != null:
			_effects.append(effect)
			effect.setup(self)

	_animation.play("reload")


func _physics_process(_delta):
	_active = _area.has_overlapping_bodies()


func _process(_delta):
	_power.power_required = self.data.value
	_animation.speed_scale = ANIMATION_TIME / self.data.speed


func _on_received_power(amount, _delta):
	if amount >= _power.power_required and _active:
		_activate()


func _activate():
	if _animation.is_playing():
		return

	_animation.play("activate")
	await _animation.animation_finished

	_do_damage()

	_animation.play("reload")
	await _animation.animation_finished


func _do_damage():
	if not _sound_effect.is_empty():
		SoundManager.play(_sound_effect)

	for area in _area.get_overlapping_areas():
		var hurt_box = area as HurtBoxComponent
		if hurt_box != null:
			hurt_box.take_damage(self.data.damage, global_position)

			for effect in _effects:
				effect._applyEffect(hurt_box.owner)
