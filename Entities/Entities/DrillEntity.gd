class_name DrillEntity
extends Entity

const ANIMATION_TIME = 4.0
const DESTRICTION_TIME = 12.0 / 19.0  # destction happen on 12th frame

@onready var _area: Area2D = $Area2D

@onready var DrillScene = preload("res://Entities/Entities/Drill.tscn")

var _components: Array[DestructionComponent]
var _worker_size := 1
var _worker := 0
var _is_working := false


func _process(_delta):
	_worker_size = self.data.value


func _physics_process(_delta):
	if _is_working:
		return

	_components = _find_close_desctruction_components()
	if len(_components) > 0:
		_is_working = true
		_start_destruction()


func _find_close_desctruction_components() -> Array[DestructionComponent]:
	var components: Array[DestructionComponent] = []

	for body in _area.get_overlapping_areas():
		var destruction := body.get_node_or_null("DestructionComponent") as DestructionComponent
		if destruction != null:
			components.append(destruction)

		if len(components) >= _worker_size:
			break

	return components


func _start_destruction() -> void:
	var destruction_time = DESTRICTION_TIME * ANIMATION_TIME / self.data.speed
	var animation_scale = self.data.speed

	while not _components.is_empty():
		var component = _components.pop_front()
		var worker := DrillScene.instantiate() as Drill

		worker.work_done.connect(_on_woker_done)
		_worker += 1

		add_child(worker)

		worker.set_animation_speed(animation_scale)
		worker.destruct(component, self.data.damage, destruction_time)


func _on_woker_done() -> void:
	_worker -= 1
	if _worker <= 0:
		_is_working = false
