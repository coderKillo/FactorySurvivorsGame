extends Entity

const OVERHEAT_TIME = 20  # sek
const HEAT_PER_SEK = 10

@onready var _heat_provider: HeatProvider = $HeatProvider
@onready var _heat_receiver: HeatReceiver = $HeatReceiver
@onready var _fire: AnimatedSprite2D = $FireAnimation
@onready var _ore_bucket: Bucket = $OreBucket
@onready var _heat_bucket: Bucket = $HeatBucket
@onready var _molt_bucket: Bucket = $MoltBucket
@onready var _smoke: GPUParticles2D = $Smoke


func _ready():
	_heat_bucket._limit = OVERHEAT_TIME * HEAT_PER_SEK

	_heat_receiver.matieral_provided.connect(_on_heat_provided)

	Events.system_tick.connect(_on_system_tick)


func _on_material_enter_area_body_entered(body: Node2D):
	if not body.is_in_group("ore"):
		return

	var entity := body as GroundEntity
	if not entity:
		return

	if _ore_bucket.full():
		return

	_ore_bucket.put(entity.data.value)
	entity.queue_free()


func _on_system_tick(delta):
	_smoke.emitting = false

	if _heat_bucket.full():
		_smoke.emitting = true
		_fire.hide()
		return

	if _ore_bucket.empty() and _molt_bucket.empty():
		_fire.hide()
		return

	if _molt_bucket.full():
		_heat_receiver.required_heat = 0
	else:
		_heat_receiver.required_heat = self.data.amount
		_provide_molt()

	_produce_heat()
	_update_overheat(delta)

	_fire.show()
	SoundManager.play("smelter_active")


func _on_heat_provided(amount: int) -> void:
	if _molt_bucket.full():
		return
	_molt_bucket.put(amount)


func _provide_molt() -> void:
	_molt_bucket.put(_ore_bucket.take(self.data.value))


func _produce_heat() -> void:
	_heat_provider.amount += _molt_bucket.take(self.data.amount)


func _update_overheat(delta) -> void:
	if self.data.upgrade_1:
		if _molt_bucket.full():
			_heat_bucket.put(HEAT_PER_SEK * delta)
	else:
		_heat_bucket.put(HEAT_PER_SEK * delta)
