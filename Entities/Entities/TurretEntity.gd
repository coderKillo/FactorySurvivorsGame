class_name TurretEntity
extends Entity

const ANIMATION_TIME = 1.0

@export var ProjectileScene = preload("res://Systems/Weapon/ProjectileBig.tscn")

@onready var _area: Area2D = $Area2D
@onready var _power: PowerReceiver = $PowerReceiver
@onready var _shoot_position: Marker2D = $Marker2D
@onready var _animation: AnimatedSprite2D = $AnimatedSprite2D
@onready var _projectiles: Node2D = $Projectiles

var _is_attacking := false
var _active := false
var _target: Node2D


func _ready():
	_animation.play("idle")
	_power.received_power.connect(_on_received_power)


func _physics_process(_delta):
	_active = _area.has_overlapping_areas()

	if not _area.has_overlapping_areas():
		return

	if _target == null:
		_target = _area.get_overlapping_areas().pick_random()

	_face_target()


func _face_target():
	var direction = global_position.direction_to(_target.global_position)
	var x = _shoot_position.position.x

	_shoot_position.look_at(_target.global_position)

	if direction.x > 0:
		_animation.flip_h = false
		_shoot_position.position.x = -x if x < 0 else x
	elif direction.x < 0:
		_animation.flip_h = true
		_shoot_position.position.x = -x if x > 0 else x

	if direction.x * _shoot_position.position.x < 0:
		_shoot_position.position.x *= -1


func _process(_delta):
	_animation.speed_scale = ANIMATION_TIME / self.data.speed
	_power.power_required = self.data.energy_cost


func _on_received_power(amount, _delta):
	if amount >= _power.power_required and _active:
		_attack()


func _attack():
	if _is_attacking:
		return

	_is_attacking = true
	_animation.play("anticipation")
	await _animation.animation_finished

	if _target != null:
		_fire()

	_animation.play("recovery")
	await _animation.animation_finished

	_is_attacking = false
	_animation.play("idle")


func _fire():
	var base_spread := 15
	# 1:0 2:-7.5 3:-15 3:-22,5 ...
	var spread := -((self.data.amount - 1) / 2.0) * base_spread

	for _i in self.data.amount:
		var projectile: Projectile = ProjectileScene.instantiate()
		projectile.transform = _shoot_position.transform
		projectile.damage = self.data.damage

		projectile.rotation_degrees += spread
		spread += base_spread

		_projectiles.add_child(projectile)

	SoundManager.play("turret_fire")
