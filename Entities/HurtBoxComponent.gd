class_name HurtBoxComponent
extends Area2D

signal hit(damage: int)
signal push_back(direction: Vector2)

@export var health: Health
@export var hit_sound := ""
@export var damage_color := Color.WHITE

var can_take_damage = true


func _ready() -> void:
	assert(health, "missing health component")


func take_damage(damage: int, source_position: Vector2):
	if not can_take_damage:
		return

	damage = randi_range(int(damage * 0.8), damage)

	if hit_sound != "":
		SoundManager.play(hit_sound)

	var hit_direction = global_position.direction_to(source_position).normalized()

	Events.frame_freeze.emit()
	Events.camera_shake.emit(1.0)
	Events.spawn_effect_rotated.emit(
		"hit_effect", global_position, rad_to_deg(hit_direction.angle())
	)

	DamageNumbers.display(damage, global_position, damage_color)

	hit.emit(damage)
	push_back.emit(-hit_direction)

	health.points -= damage
