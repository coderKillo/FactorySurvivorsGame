@tool
class_name UpgradeValue
extends Resource

enum Operation {
	ADD,
	SUBTRACT,
	MULTIPLY,
	DIVIDE,
	ASSIGN,
}

@export var path := ""
@export var operation := Operation.ADD
@export var type := TYPE_INT:
	set(value):
		type = value
		property_list_changed.emit()

var upgrade_value: Variant


func _get_property_list():
	return [{name = "upgrade_value", type = type}]


func apply(object: Object) -> void:
	object.set_indexed(path, get_new_value(object.get_indexed(path)))


func is_assign() -> bool:
	return operation == Operation.ASSIGN


func get_new_value(old_value: Variant) -> Variant:
	match operation:
		Operation.ADD:
			return old_value + upgrade_value

		Operation.SUBTRACT:
			return max(upgrade_value, old_value - upgrade_value)

		Operation.MULTIPLY:
			return old_value * upgrade_value

		Operation.DIVIDE:
			return old_value / upgrade_value

		Operation.ASSIGN:
			return upgrade_value

	return null
