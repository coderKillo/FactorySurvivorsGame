class_name UpgradeSystem
extends RefCounted

const UPGRADE_OPTIONS = 3
const UPGRADE_PATH = "res://Systems/Upgrade/upgrades/"

var _player: Player
var _entity_tracker: EntityTracker
var _gui: GUI

var _current_requirements: Dictionary = {}

# DECKS
var _unused_deck: Array[Upgrade] = []
var _used_deck: Array[Upgrade] = []
var _locked_deck: Array[Upgrade] = []
var _unlock_queue: Array[Upgrade] = []
var _hand: Array[Upgrade] = []


func setup(player: Player, entity_tracker: EntityTracker, gui: GUI):
	_player = player
	_entity_tracker = entity_tracker
	_gui = gui

	_load_upgrades()

	Events.leveled_up.connect(_on_level_up)


func _load_upgrades():
	var dir = DirAccess.open(UPGRADE_PATH)
	if not dir:
		print("Error occured when try to access: ", UPGRADE_PATH)
		return

	dir.list_dir_begin()

	var filename := dir.get_next()
	while filename != "":
		if filename.begins_with("upgrade_"):
			if filename.ends_with(".remap"):
				filename = filename.trim_suffix(".remap")
			var upgrade := load(dir.get_current_dir() + "/" + filename) as Upgrade

			if upgrade == null:
				printerr("fail to load: " + filename)
			elif upgrade.requirements.is_empty():
				_unused_deck.append(upgrade)
			else:
				_locked_deck.append(upgrade)

		filename = dir.get_next()


func _on_level_up() -> void:
	for upgrade in _locked_deck.duplicate():
		if _requirements_fulfilled(upgrade):
			_locked_deck.erase(upgrade)
			_unlock_queue.append(upgrade)

	_hand.clear()
	_used_deck.shuffle()
	_unused_deck.shuffle()

	# 1. Card
	if not _unused_deck.is_empty():
		_hand.append(_unused_deck.pop_back())
	else:
		_hand.append(_used_deck.pop_back())

	# 2. Card
	if not _unused_deck.is_empty():
		_hand.append(_unused_deck.pop_back())
	else:
		_hand.append(_used_deck.pop_back())

	# 3. Card
	if not _unlock_queue.is_empty():
		_hand.append(_unlock_queue.pop_back())
	elif not _used_deck.is_empty():
		_hand.append(_used_deck.pop_back())
	else:
		_hand.append(_unused_deck.pop_back())

	_hand.shuffle()
	_gui.display_upgrade(_hand, _apply_upgrade)


func _apply_upgrade(upgrade: Upgrade) -> void:
	_hand.erase(upgrade)
	if not upgrade.unique:
		_used_deck.append(upgrade)

	_unused_deck.append_array(_hand)
	_hand.clear()

	_add_requriement(upgrade.type)
	_add_requriement(upgrade.object)
	_add_tree_requriement(upgrade.type, upgrade.object)

	_add_explosion_effect(upgrade)

	if upgrade.type == "entity" and upgrade.title.begins_with("New:"):
		_gui.add_to_quickbar(Library.blueprints[upgrade.object].instantiate())
		return

	var object := UpgradeData.get_update_object(upgrade.type, upgrade.object)
	upgrade.upgrade(object)

	Events.upgrade_data_changed.emit()


func _add_explosion_effect(upgrade: Upgrade) -> void:
	if upgrade.type == "player" or upgrade.type == "weapon":
		Events.spawn_effect.emit("upgrade_explosion", _player.global_position)

	elif upgrade.type == "entity":
		var entity_name := upgrade.object
		for location in _entity_tracker.entities.keys():
			var entity := _entity_tracker.entities[location] as Entity
			if Library.get_entity_name(entity) == entity_name:
				Events.spawn_effect.emit("upgrade_explosion", entity.global_position)


# add requirements depend on a virtual skill tree
func _add_tree_requriement(type: String, object: String):
	if (
		type == "player"
		or object == "PowerPlant"
		or object == "Conveyor"
		or object == "Smelter"
		or object == "Wire"
	):
		_add_requriement("energy")
	if object in ["pickaxe", "Drill", "Crusher"]:
		_add_requriement("mining")
	if object in ["blaster", "SpikeTrap", "Turret", "ExplosiveTrap"]:
		_add_requriement("damage")


func _add_requriement(type: String) -> void:
	if not _current_requirements.has(type):
		_current_requirements[type] = 0
	_current_requirements[type] += 1


func _requirements_fulfilled(upgrade: Upgrade) -> bool:
	for requirement in upgrade.requirements:
		if requirement not in _current_requirements:
			return false
		if _current_requirements[requirement] < upgrade.requirements[requirement]:
			return false
	return true
