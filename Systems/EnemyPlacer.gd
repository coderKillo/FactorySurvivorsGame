class_name EnemyPlacer
extends Node2D

@export var time_between_waves := 15.0  # sek
@export var time_between_enemies := 0.1
@export var spawn_distance := 10
@export var start_credits := 100
@export var credits_per_level := 50

var _player: Node2D = null
var _monster_builder := MonsterBuilder.new()
var _current_enemy_count := 0
var _current_credits := 0

@onready var _spawn_timer: Timer = $SpawnTimer


func _ready():
	_current_credits = start_credits

	randomize()
	_spawn_timer.timeout.connect(_on_spawn_timer_timeout)
	Events.enemy_died.connect(_on_enemy_died)


func setup(player: Node2D):
	_player = player
	_start()


func _start() -> void:
	_spawn_wave()


func _on_spawn_timer_timeout():
	_spawn_wave()


func _on_enemy_died(enemy):
	_spawn_enemy_corps(enemy)

	_current_enemy_count -= 1
	if _current_enemy_count <= 0:
		_current_credits += credits_per_level

		Events.enemies_attack.emit(false)
		Events.leveled_up.emit()

		_spawn_timer.start(time_between_waves)


func _spawn_enemy_corps(enemy: Enemy) -> void:
	var corps: Entity = Library.entites.EnemyCorps.instantiate()

	corps.global_position = enemy.global_position

	var destruction_component := (
		corps.get_node_or_null("DestructionComponent") as DestructionComponent
	)
	if destruction_component != null:
		destruction_component.ore_color = enemy.color
		destruction_component.health = enemy.health.max_health

	for sprite in enemy.model.get_death_sprite():
		corps.add_child(sprite)

	add_child(corps)


func _spawn_wave():
	if _player == null:
		printerr("no player found")
		return

	Events.enemies_attack.emit(true)

	var spawn_angle = randi() % 360
	var enemies = _monster_builder.create_monster_wave(_current_credits)

	for enemy in enemies:
		await get_tree().create_timer(time_between_enemies).timeout

		var angle = randi_range(spawn_angle - 30, spawn_angle + 30)
		var spawn_pos = _player.position + (Vector2.from_angle(deg_to_rad(angle)) * spawn_distance)

		enemy.position = spawn_pos
		enemy.target = _player

		add_child(enemy)

		_current_enemy_count += 1
		Events.enemy_spawn.emit(enemy)
