class_name PipeSystem
extends Node2D

@onready var _paths: PipePaths = $TileMap
@onready var _pathfinder: PipePathFinder = $PipePathFinder
@onready var _preview: PipePathPreview = $PipePathPreview
@onready var _distributor: PipeHeatDistributor = $PipeHeatDistributor

var _entity_tracker: EntityTracker

var _build_mode := BuildModeManager.GameMode.NORMAL_MODE

var _start_point = null
var _current_cell_under_mouse := Vector2.ZERO


func setup(entity_tracker: EntityTracker) -> void:
	_entity_tracker = entity_tracker

	_preview.setup(_paths)
	_distributor.setup(_paths)


func _ready():
	Events.build_mode_changed.connect(_on_build_mode_changed)


func _input(event: InputEvent):
	if event.is_action_pressed("place_entity"):
		if _get_entity_name_under_mouse() == "Smelter":
			_start_point = _paths.get_cell_under_mouse()
			_current_cell_under_mouse = _start_point

			update_preview_path()

	if event.is_action_released("place_entity"):
		if _get_entity_name_under_mouse() == "PowerPlant":
			add_path()

		_start_point = null
		_preview.clear()

		update_preview_path()

	if _current_cell_under_mouse != _paths.get_cell_under_mouse():
		_current_cell_under_mouse = _paths.get_cell_under_mouse()

		update_preview_path()


func _on_build_mode_changed(mode: BuildModeManager.GameMode) -> void:
	_build_mode = mode

	update_preview_path()


func _get_entity_name_under_mouse() -> String:
	var current_mouse_cell = _paths.get_cell_under_mouse()

	if current_mouse_cell in _entity_tracker.blocked_cells:
		return _entity_tracker.blocked_cells[current_mouse_cell]

	if not current_mouse_cell in _entity_tracker.entities.keys():
		return ""

	var entity := _entity_tracker.entities[current_mouse_cell] as Entity
	if entity == null:
		return ""

	return Library.get_entity_name(entity)


func update_preview_path() -> void:
	if _build_mode != BuildModeManager.GameMode.BUILD_MODE:
		_preview.clear()
		return

	if _start_point == null:
		_preview.clear()
		return

	_preview.path = _pathfinder.find_path(_start_point, _current_cell_under_mouse)


func add_path() -> void:
	if _preview.path.is_empty():
		return

	_paths.add(_preview.path)
