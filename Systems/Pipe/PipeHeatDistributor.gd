class_name PipeHeatDistributor
extends Node2D

var _heat_receivers = {}
var _heat_providers = {}

var _paths: PipePaths

var _queue_repathing := false
var _heat_connections := {}  # Position of provider to receiver list


func _init():
	Events.entity_placed.connect(_on_entity_placed)
	Events.entity_removed.connect(_on_entity_removed)
	Events.system_tick.connect(_on_system_tick)


func setup(paths: PipePaths) -> void:
	_paths = paths
	_paths.paths_changed.connect(_on_paths_changed)


func _on_entity_placed(entity: Entity, cellv: Vector2):
	if entity.is_in_group(Types.HEAT_PROVIDER):
		_heat_providers[cellv] = entity.get_node_or_null("HeatProvider")
		_queue_repathing = true

	if entity.is_in_group(Types.HEAT_RECEIVER):
		_heat_receivers[cellv] = entity.get_node_or_null("HeatReceiver")
		_queue_repathing = true


func _on_entity_removed(_entity: Entity, cellv: Vector2):
	_heat_receivers.erase(cellv)
	if _heat_providers.erase(cellv):
		_queue_repathing = true


func _on_paths_changed():
	_queue_repathing = true


func _on_system_tick(_delta):
	if _queue_repathing:
		_repath()
		_queue_repathing = false

	_distribute_heat()


func _distribute_heat() -> void:
	for provider_pos in _heat_connections.keys():
		var heat_used = 0
		var provider = _heat_providers[provider_pos] as HeatProvider
		var receiver_count = len(_heat_connections[provider_pos])

		for receiver_pos in _heat_connections[provider_pos]:
			var receiver: HeatReceiver = _heat_receivers[receiver_pos]
			var heat_provided: int = min(
				receiver.required_heat, float(provider.amount) / receiver_count
			)

			heat_used += heat_provided
			receiver.matieral_provided.emit(heat_provided)

			DamageNumbers.display(
				heat_provided, receiver.global_position + Vector2(4, 0), Color.GREEN
			)

		provider.amount -= heat_used

		DamageNumbers.display(-heat_used, provider.global_position + Vector2(-4, 0), Color.RED)


func _repath() -> void:
	for path in _paths.get_paths():
		var _last_provider = null
		for point in path:
			if point in _heat_receivers and _last_provider != null:
				_heat_connections[_last_provider].append(point)

			if point in _heat_providers:
				_last_provider = point
				if not _last_provider in _heat_connections:
					_heat_connections[_last_provider] = []
