class_name BuildModeManager
extends Node2D

enum GameMode 
{
	NORMAL_MODE,
	BUILD_MODE
}

var _current_game_mode := GameMode.NORMAL_MODE

var _player: Player
var _gui: GUI
var _enemy_placer: EnemyPlacer
var _entity_tracker: EntityTracker
var _simulation_timer: Timer

func setup(player: Player, gui: GUI, enemy_placer: EnemyPlacer, entity_tracker: EntityTracker, simulation_timer):
	_player = player
	_gui = gui
	_enemy_placer = enemy_placer
	_entity_tracker = entity_tracker
	_simulation_timer = simulation_timer


func _unhandled_input(event):
	if event.is_action_pressed("toggle_build_mode"):
		if _current_game_mode == GameMode.NORMAL_MODE:
			_set_build_mode(true)
			_current_game_mode = GameMode.BUILD_MODE

		elif _current_game_mode == GameMode.BUILD_MODE:
			_set_build_mode(false)
			_current_game_mode = GameMode.NORMAL_MODE


func _set_build_mode(active:bool)->void:
	_simulation_timer.paused = active

	_gui.set_quickbar_visible(active)
	_gui.destroy_blueprint()
	_gui.pause_quickbar_cooldowns(active)

	_pause_scene(_player, active)
	_pause_scene(_enemy_placer, active)

	for entity in _entity_tracker.entities.values():
		_pause_scene(entity, active)


func _pause_scene(node: Node, pause:bool)->void:
	_pause_node(node, pause)
	for child in node.get_children():
		_pause_scene(child, pause)


func _pause_node(node: Node, pause:bool)->void:
	node.set_process(!pause)
	node.set_physics_process(!pause)
	node.set_process_input(!pause)
	node.set_process_internal(!pause)
	node.set_process_unhandled_input(!pause)
	node.set_process_unhandled_key_input(!pause)