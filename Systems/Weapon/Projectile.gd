class_name Projectile
extends Node2D

@export var speed = 750
@export var damage = 10
@export var cost = 10
@export var max_range = 200
@export var impact_sound := "projectile_hit"
@export var shape: Shape2D
@export var has_screenshake := false

@onready var _animation: AnimatedSprite2D = $AnimatedSprite2D

var _travel_distance := 0


func _ready():
	_animation.play("default")


func _process(delta):
	var distance = speed * delta
	var motion = transform.x * distance
	position += motion

	_travel_distance += distance

	var query := PhysicsShapeQueryParameters2D.new()
	query.set_shape(shape)
	query.collide_with_bodies = true
	query.collide_with_areas = true
	query.collision_mask = (2 + 8)
	query.transform = global_transform

	var results := get_world_2d().direct_space_state.intersect_shape(query, 1)
	if results:
		for result in results:
			var hurt_box := result["collider"] as HurtBoxComponent
			var enemy := result["collider"] as Enemy
			if hurt_box != null:
				hurt_box.take_damage(damage, global_position)
			elif enemy != null:
				enemy._hurt_box.take_damage(damage, global_position)

		_destroy_projectile()

		if has_screenshake:
			Events.frame_freeze.emit()
			Events.camera_shake.emit(1.0)

		queue_free()

	if _travel_distance >= max_range:
		queue_free()


func _destroy_projectile():
	_animation.play("impact")
	SoundManager.play(impact_sound)

	global_rotation_degrees = 0
	await _animation.animation_finished

	queue_free()
