class_name WorkComponent
extends Node2D

const INPUT_SLOT = 0
const OUTPUT_SLOT = 1

signal update_progress(percent: float)

@export var input_name := ""
@export var output_name := ""
@export var process_time := 2.0
@export var pickup_time := 1.0
@export var pickup_area: Area2D
@export var output_pos: Marker2D

var _slots: Array[InventorySlot] = [InventorySlot.new(), InventorySlot.new()]

@onready var _process_timer: Timer = $ProcessTimer
@onready var _pickup_timer: Timer = $PickUpTimer


func _ready():
	assert(input_name in Library.blueprints, "invalid input")
	assert(output_name in Library.entites, "invalid output")

	_process_timer.timeout.connect(_on_process_timer_timeout)
	_pickup_timer.timeout.connect(_on_pickup_timer_timeout)
	_pickup_timer.timeout.connect(_on_place_timer_timeout)
	_pickup_timer.start(pickup_time)


func _process(_delta):
	if not _process_timer.is_stopped():
		update_progress.emit(process_time / _process_timer.time_left)


func start() -> void:
	if !_process_timer.is_stopped():
		return
	_process_timer.start(process_time)
	update_progress.emit(0.0)


func stop() -> void:
	_process_timer.stop()
	update_progress.emit(0.0)


func has_available_work() -> bool:
	return not _slots[INPUT_SLOT].empty()


func _on_pickup_timer_timeout() -> void:
	var entity := _get_valid_input_entity()
	if entity == null:
		return

	if _slots[INPUT_SLOT].full():
		return

	_slots[INPUT_SLOT].stack += 1
	entity.queue_free()


func _on_place_timer_timeout() -> void:
	if _slots[OUTPUT_SLOT].empty():
		return

	_slots[OUTPUT_SLOT].stack -= 1
	Events.ground_entity_spawn.emit(output_name, output_pos.global_position)


func _on_process_timer_timeout() -> void:
	if _slots[INPUT_SLOT].empty() or _slots[OUTPUT_SLOT].full():
		return

	_slots[INPUT_SLOT].stack -= 1
	_slots[OUTPUT_SLOT].stack += 1


func _get_valid_input_entity() -> Entity:
	for body in pickup_area.get_overlapping_bodies():
		var entitiy_name = Library.get_entity_name(body)
		if entitiy_name == input_name:
			return body

	return null
