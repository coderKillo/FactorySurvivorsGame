class_name BaseNoiseWorldGenerator
extends WorldGenerator

const SOURCE_ID = 0
const LAYER = 0

@export var alternate_tiles: Dictionary
@export var enable_random_color: bool = false
@export var modulate_color: Gradient

@onready var _tilemap: TileMap = $TileMap
@onready var _nav_map: NavMap = $NavMap

var _default_tile := Vector2i(5, 3)


func _ready():
	if enable_random_color:
		_tilemap.modulate = modulate_color.get_color(randi() % modulate_color.get_point_count())
	generate()


func _generate_sector(x: int, y: int) -> void:
	_rng.seed = _make_new_seed(x, y)

	var cell_offset = Vector2i(x, y) * _sector_cell_count

	# fill
	for x_cell in range(0, _sector_cell_count):
		for y_cell in range(0, _sector_cell_count):
			_tilemap.set_cell(
				LAYER, Vector2i(x_cell, y_cell) + cell_offset, SOURCE_ID, _default_tile
			)
			_nav_map.set_cell(Vector2i(x_cell, y_cell) + cell_offset, "on")

	# highlights
	for source_id in alternate_tiles:
		_place_random_highlight(source_id, alternate_tiles[source_id], cell_offset)


func _place_random_highlight(_source_id: int, _occurence: int, _offset: Vector2i):
	pass


func _clear_sector(x: int, y: int) -> void:
	var cell_offset = Vector2i(x, y) * _sector_cell_count

	# fill
	for x_cell in range(0, _sector_cell_count):
		for y_cell in range(0, _sector_cell_count):
			_tilemap.erase_cell(LAYER, Vector2i(x_cell, y_cell) + cell_offset)
