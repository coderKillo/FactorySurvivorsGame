class_name WorldGenerator
extends Node2D

enum Axis { AXIS_X, AXIS_Y }

@export var sector_size := 480
@export var sector_axis_count := 6
@export var quadrad_cell_size := 16
@export var start_seed := "world_generator"

var _sectors := {}
var _current_sector := Vector2.ZERO
var _rng := RandomNumberGenerator.new()
var _target: Node2D

@onready var _half_sector_size := sector_size / 2.0
@onready var _half_sector_count := int(sector_axis_count / 2.0)
@onready var _sector_cell_count := int(sector_size / quadrad_cell_size)
@onready var _total_sector_size := sector_size * sector_size


func setup(target: Node2D):
	_target = target


func generate() -> void:
	for x in range(-_half_sector_count + _current_sector.x, _half_sector_count + _current_sector.x):
		for y in range(
			-_half_sector_count + _current_sector.y, _half_sector_count + _current_sector.y
		):
			_generate_sector(x, y)


# override this
func _generate_sector(_x: int, _y: int) -> void:
	pass


# override this
func _clear_sector(_x: int, _y: int) -> void:
	pass


func _make_new_seed(x: int, y: int, content: String = "") -> int:
	var seed_string := "%s_%s_%s" % [start_seed, x, y]
	if content:
		seed_string = "%s_%s" % [seed_string, content]

	return seed_string.hash()


func _process(_delta):
	if _target == null:
		return

	var target_sector = _get_sector(_target.global_position)

	if target_sector != _current_sector:
		var direction = target_sector - _current_sector
		_move_sector(direction)


func _get_sector(pos: Vector2) -> Vector2:
	var x = floor(pos.x / sector_size)
	var y = floor(pos.y / sector_size)
	return Vector2(x, y)


func _move_sector(direction: Vector2) -> void:
	if direction.x != 0:
		var left = int(_current_sector.x) - _half_sector_count
		var right = int(_current_sector.x) + _half_sector_count - 1
		var up = int(_current_sector.y) - _half_sector_count
		var down = int(_current_sector.y) + _half_sector_count - 1

		for y in range(up, down + 1):
			if direction.x > 0:
				_generate_sector(right + 1, y)
				_clear_sector(left, y)
			if direction.x < 0:
				_generate_sector(left - 1, y)
				_clear_sector(right, y)

		_current_sector.x += direction.x

	if direction.y != 0:
		var left = int(_current_sector.x) - _half_sector_count
		var right = int(_current_sector.x) + _half_sector_count - 1
		var up = int(_current_sector.y) - _half_sector_count
		var down = int(_current_sector.y) + _half_sector_count - 1

		for x in range(left, right + 1):
			if direction.y > 0:
				_generate_sector(x, down + 1)
				_clear_sector(x, up)
			if direction.y < 0:
				_generate_sector(x, up - 1)
				_clear_sector(x, down)

		_current_sector.y += direction.y
