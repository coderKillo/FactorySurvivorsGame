extends Node

@export var simulation_speed = 1.0 / 10.0

var _tracker := EntityTracker.new()
@warning_ignore("unused_variable")
@warning_ignore("unused_private_class_variable")
var _power_system := PowerSystem.new()
var _upgrade_system := UpgradeSystem.new()

@onready var PlayerScene := preload("res://Entities/Player/Player.tscn")
@onready var CartScene := preload("res://Entities/Minecart.tscn")
@onready var DropPodScene: PackedScene = preload("res://Entities/DropPod.tscn")

@onready var _entity_placer = $GameWorld/EntityPlacer
@onready var _enemy_placer = $GameWorld/EnemyPlacer
@onready var _world_generator: WorldGenerator = $GameWorld/WorldGenerator
@onready var _pipe_system: PipeSystem = $GameWorld/PipeSystem
@onready var _gui: GUI = $CanvasLayer/GUI
@onready var _build_mode_manager: BuildModeManager = $BuildModeManager


func _ready():
	var planet_data = PlanetData.current_planet()
	_set_world_color(planet_data.color)

	var simulation_timer = $SimulationTimer

	var player = PlayerScene.instantiate()
	var cart = CartScene.instantiate()

	_entity_placer.setup(_gui, _tracker, player)
	_enemy_placer.setup(player)
	_world_generator.setup(player)
	_pipe_system.setup(_tracker)
	_upgrade_system.setup(player, _tracker, _gui)
	_build_mode_manager.setup(player, _gui, _enemy_placer, _tracker, simulation_timer)
	simulation_timer.start(simulation_speed)
	simulation_timer.timeout.connect(_on_SimulationTimer_timeout)
	Events.player_died.connect(_on_player_death)

	_drop_entity(player, Vector2(0, 0))
	_drop_entity(cart, Vector2(-60, -30))


func _on_SimulationTimer_timeout() -> void:
	Events.system_tick.emit(simulation_speed)
	_tracker.remove_queued_entity()


func _set_world_color(color_name: String) -> void:
	_world_generator.material.set_shader_parameter(
		"palette", RecolorTable._get_color_platte_from(color_name)
	)


func _drop_entity(entity: Node2D, location: Vector2) -> void:
	entity.visible = false
	entity.position = location
	$GameWorld.add_child(entity)

	var drop_pod := DropPodScene.instantiate() as DropPod
	drop_pod.global_position = location
	add_child(drop_pod)

	await drop_pod.opened

	entity.visible = true


func _on_player_death() -> void:
	var current_resources = PlanetData.current_planet().resources
	var new_resource = current_resources - _power_system.total_power()
	PlanetData.current_planet().resources = max(new_resource, 0)
